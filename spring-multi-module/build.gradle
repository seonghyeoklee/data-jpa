plugins {
    id 'org.springframework.boot' version '2.7.5'
    id 'java'
}

repositories {
    mavenCentral()
}

bootJar.enabled = false

// 프로젝트 공통 세팅
subprojects {
    group = 'com.example'
    version = '0.1'
    sourceCompatibility = '17'

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    // 공통으로 사용하는 의존성
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-webflux'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        runtimeOnly 'com.h2database:h2'
    }

    test {
        useJUnitPlatform()
    }

    final String DEFAULT_PROFILE = 'local'
    allprojects {
        if (!project.hasProperty('profile') || !profile) {
            ext.profile = DEFAULT_PROFILE
        }

        sourceSets {
            main {
                resources {
                    srcDir "src/main/resources"
                    srcDir "src/main/resources-${profile}"
                }
            }
        }
    }
}

project(':api-service') {
    bootJar { enabled = true }
    jar { enabled = false }

    dependencies {
        implementation project(':core-service')
    }
}

project(':core-service') {
    bootJar { enabled = false }
    jar { enabled = true }

    dependencies {
    }
}
